buildscript {
    ext.kotlin_version = '1.3.61'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "maven-publish"
    id "maven"
    id "java"
    id "jacoco"
}
apply plugin: 'kotlin'

group 'com.github.korosuke613'
version = System.getenv("GRADLE_PUBLISH_VERSION") ?: "1.0.0"

project.ext {
    artifactId = 'pict4java'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile group: 'net.java.dev.jna', name: 'jna', version: '5.5.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    implementation 'org.scijava:native-lib-loader:2.3.4'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/customJacocoReportDir")
}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/korosuke613/pict4java")
            credentials {
                username = System.getenv("GITHUB_PACKAGE_USERNAME")
                password = System.getenv("GITHUB_PACKAGE_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
